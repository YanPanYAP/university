-- Создание PDB CRS_PDB
CREATE PLUGGABLE DATABASE CRS_PDB
  ADMIN USER crs_admin IDENTIFIED BY Oracle123
  FILE_NAME_CONVERT = ('pdbseed', 'crs_pdb');

-- Открытие PDB и сохранение состояния
ALTER PLUGGABLE DATABASE CRS_PDB OPEN;
ALTER PLUGGABLE DATABASE CRS_PDB SAVE STATE;

-- Проверка PDB
SELECT name, open_mode FROM v$pdbs;

-- Переключение в CRS_PDB
ALTER SESSION SET CONTAINER=CRS_PDB;

--------------------------------------------------------------------------------
-- Создание таблиц, индексов, привелегий
--------------------------------------------------------------------------------

CREATE OR REPLACE PROCEDURE create_all_tables IS
BEGIN
  -- USERS
  EXECUTE IMMEDIATE '
    CREATE TABLE USERS (
      user_id     NUMBER GENERATED BY DEFAULT ON NULL AS IDENTITY PRIMARY KEY,
      login       VARCHAR2(50) NOT NULL,
      email       VARCHAR2(100) NOT NULL,
      created_at  DATE DEFAULT SYSDATE
    )';

  -- ADMINS
  EXECUTE IMMEDIATE '
    CREATE TABLE ADMINS (
      admin_id   NUMBER GENERATED BY DEFAULT ON NULL AS IDENTITY PRIMARY KEY,
      user_id    NUMBER NOT NULL,
      role_name  VARCHAR2(20) NOT NULL,
      CONSTRAINT fk_admin_user FOREIGN KEY (user_id) REFERENCES USERS(user_id),
      CONSTRAINT chk_admin_role CHECK (role_name IN (''GLAV_ADMIN'', ''ADMIN''))
    )';

  -- STUDIOS
  EXECUTE IMMEDIATE '
    CREATE TABLE STUDIOS (
      studio_id      NUMBER GENERATED BY DEFAULT ON NULL AS IDENTITY PRIMARY KEY,
      name           VARCHAR2(100) NOT NULL,
      description    CLOB,
      price_per_hour NUMBER(8,2) NOT NULL,
      square         NUMBER(6)
    )';

  -- STUDIO_RENT_ORDERS
  EXECUTE IMMEDIATE '
    CREATE TABLE STUDIO_RENT_ORDERS (
      order_id    NUMBER GENERATED BY DEFAULT ON NULL AS IDENTITY PRIMARY KEY,
      user_id     NUMBER NOT NULL,
      studio_id   NUMBER NOT NULL,
      rent_date   DATE NOT NULL,
      start_time  TIMESTAMP NOT NULL,
      end_time    TIMESTAMP NOT NULL,
      total_price NUMBER(10,2) NOT NULL,
      status      VARCHAR2(20) DEFAULT ''PENDING''
                   CHECK (status IN (''PENDING'',''APPROVED'',''CANCELLED'')),

      CONSTRAINT fk_sro_user FOREIGN KEY (user_id) REFERENCES USERS(user_id),
      CONSTRAINT fk_sro_studio FOREIGN KEY (studio_id) REFERENCES STUDIOS(studio_id),
      CONSTRAINT chk_rent_time CHECK (end_time > start_time)
    )';

  -- ARCHIVED_STUDIO_RENT_ORDERS
  EXECUTE IMMEDIATE '
    CREATE TABLE ARCHIVED_STUDIO_RENT_ORDERS (
      order_id    NUMBER GENERATED BY DEFAULT ON NULL AS IDENTITY PRIMARY KEY,
      user_id     NUMBER NOT NULL,
      studio_id   NUMBER NOT NULL,
      rent_date   DATE NOT NULL,
      start_time  TIMESTAMP NOT NULL,
      end_time    TIMESTAMP NOT NULL,
      total_price NUMBER(10,2) NOT NULL,
      archived_at DATE DEFAULT SYSDATE,

      CONSTRAINT fk_asro_user FOREIGN KEY (user_id) REFERENCES USERS(user_id),
      CONSTRAINT fk_asro_studio FOREIGN KEY (studio_id) REFERENCES STUDIOS(studio_id)
    )';

  -- SERVICES
  EXECUTE IMMEDIATE '
    CREATE TABLE SERVICES (
      service_id  NUMBER GENERATED BY DEFAULT ON NULL AS IDENTITY PRIMARY KEY,
      name        VARCHAR2(100) NOT NULL,
      price       NUMBER(8,2) NOT NULL,
      description CLOB
    )';

  -- ORDER_SERVICES
  EXECUTE IMMEDIATE '
    CREATE TABLE ORDER_SERVICES (
      order_id   NUMBER NOT NULL,
      service_id NUMBER NOT NULL,

      CONSTRAINT pk_order_services PRIMARY KEY (order_id, service_id),
      CONSTRAINT fk_os_order FOREIGN KEY (order_id)
        REFERENCES STUDIO_RENT_ORDERS(order_id),
      CONSTRAINT fk_os_service FOREIGN KEY (service_id)
        REFERENCES SERVICES(service_id)
    )';

  -- ARCHIVED_ORDER_SERVICES
  EXECUTE IMMEDIATE '
    CREATE TABLE ARCHIVED_ORDER_SERVICES (
      order_id   NUMBER NOT NULL,
      service_id NUMBER NOT NULL,
      CONSTRAINT pk_archived_order_services
        PRIMARY KEY (order_id, service_id)
    )';

  -- INDEXES
  EXECUTE IMMEDIATE 'CREATE INDEX idx_sro_user ON STUDIO_RENT_ORDERS(user_id)';
  EXECUTE IMMEDIATE 'CREATE INDEX idx_sro_studio ON STUDIO_RENT_ORDERS(studio_id)';
  EXECUTE IMMEDIATE 'CREATE INDEX idx_sro_rent_date ON STUDIO_RENT_ORDERS(rent_date)';
  EXECUTE IMMEDIATE 'CREATE INDEX idx_os_order ON ORDER_SERVICES(order_id)';
  EXECUTE IMMEDIATE 'CREATE INDEX idx_os_service ON ORDER_SERVICES(service_id)';
  EXECUTE IMMEDIATE 'CREATE INDEX idx_asro_user ON ARCHIVED_STUDIO_RENT_ORDERS(user_id)';
  EXECUTE IMMEDIATE 'CREATE INDEX idx_asro_studio ON ARCHIVED_STUDIO_RENT_ORDERS(studio_id)';
  EXECUTE IMMEDIATE 'CREATE INDEX idx_asro_rent_date ON ARCHIVED_STUDIO_RENT_ORDERS(rent_date)';
  EXECUTE IMMEDIATE 'CREATE INDEX SRO_IX_OVERLAP ON STUDIO_RENT_ORDERS (STUDIO_ID, RENT_DATE, STATUS, END_TIME, START_TIME)';
  EXECUTE IMMEDIATE 'CREATE INDEX SRO_IX_ARCHIVE ON STUDIO_RENT_ORDERS (END_TIME, STATUS)';

  -- GRANTS
  EXECUTE IMMEDIATE 'GRANT SELECT, INSERT, UPDATE, DELETE ON USERS TO CRS_CUR';
  EXECUTE IMMEDIATE 'GRANT SELECT, INSERT, UPDATE, DELETE ON STUDIOS TO CRS_CUR';
  EXECUTE IMMEDIATE 'GRANT SELECT, INSERT, UPDATE, DELETE ON STUDIO_RENT_ORDERS TO CRS_CUR';
  EXECUTE IMMEDIATE 'GRANT SELECT, INSERT, UPDATE, DELETE ON ARCHIVED_STUDIO_RENT_ORDERS TO CRS_CUR';
  EXECUTE IMMEDIATE 'GRANT SELECT, INSERT, UPDATE, DELETE ON SERVICES TO CRS_CUR';
  EXECUTE IMMEDIATE 'GRANT SELECT, INSERT, UPDATE, DELETE ON ORDER_SERVICES TO CRS_CUR';
  EXECUTE IMMEDIATE 'GRANT SELECT, INSERT, UPDATE, DELETE ON ARCHIVED_ORDER_SERVICES TO CRS_CUR';
  EXECUTE IMMEDIATE 'GRANT SELECT, INSERT, UPDATE, DELETE ON ADMINS TO CRS_CUR';

END create_all_tables;
/

BEGIN
  create_all_tables;
END;
/
--------------------------------------------------------------------------------
-- Заполнение таблиц 
--------------------------------------------------------------------------------

-- Генерация пользователей
CREATE OR REPLACE PROCEDURE gen_users IS
BEGIN
  FOR i IN 1..100000 LOOP
    INSERT INTO USERS (login, email)
    VALUES ('user' || i, 'user' || i || '@example.com');
  END LOOP;
  COMMIT;
END;
/

-- Генерация студий
CREATE OR REPLACE PROCEDURE gen_studios IS
BEGIN
  FOR i IN 1..100 LOOP
    INSERT INTO STUDIOS (name, description, price_per_hour, square)
    VALUES (
      'Studio ' || i,
      'Description for studio ' || i,
      DBMS_RANDOM.VALUE(50, 500),
      DBMS_RANDOM.VALUE(20, 200)
    );
  END LOOP;
  COMMIT;
END;
/

-- Генерация услуг
CREATE OR REPLACE PROCEDURE gen_services IS
BEGIN
  FOR i IN 1..50 LOOP
    INSERT INTO SERVICES (name, price, description)
    VALUES (
      'Service ' || i,
      DBMS_RANDOM.VALUE(10, 200),
      'Description for service ' || i
    );
  END LOOP;
  COMMIT;
END;
/

-- Генерация заказов
CREATE OR REPLACE PROCEDURE gen_studio_orders IS
  v_user_id   NUMBER;
  v_studio_id NUMBER;

  TYPE t_user_tab IS TABLE OF NUMBER INDEX BY PLS_INTEGER;
  TYPE t_studio_tab IS TABLE OF NUMBER INDEX BY PLS_INTEGER;

  l_user_ids   t_user_tab;
  l_studio_ids t_studio_tab;
BEGIN
  SELECT user_id BULK COLLECT INTO l_user_ids FROM USERS;
  SELECT studio_id BULK COLLECT INTO l_studio_ids FROM STUDIOS;

  FOR i IN 1..100000 LOOP
    v_user_id   := l_user_ids(MOD(i, l_user_ids.COUNT) + 1);
    v_studio_id := l_studio_ids(MOD(i, l_studio_ids.COUNT) + 1);

    INSERT INTO STUDIO_RENT_ORDERS
      (user_id, studio_id, rent_date, start_time, end_time, total_price, status)
    VALUES (
      v_user_id,
      v_studio_id,
      SYSDATE - MOD(i, 365),
      SYSTIMESTAMP,
      SYSTIMESTAMP + INTERVAL '2' HOUR,
      DBMS_RANDOM.VALUE(100, 2000),
      CASE MOD(i,3)
        WHEN 0 THEN 'PENDING'
        WHEN 1 THEN 'APPROVED'
        ELSE 'CANCELLED'
      END
    );
  END LOOP;

  COMMIT;
END;
/

-- Генерация связей заказов и услуг
CREATE OR REPLACE PROCEDURE gen_order_services IS
  v_service_id NUMBER;

  TYPE t_service_tab IS TABLE OF NUMBER INDEX BY PLS_INTEGER;
  TYPE t_assigned_tab IS TABLE OF NUMBER INDEX BY PLS_INTEGER;

  l_service_ids t_service_tab;
  l_assigned    t_assigned_tab;

  v_max_services PLS_INTEGER;
BEGIN
  SELECT service_id BULK COLLECT INTO l_service_ids FROM SERVICES;
  v_max_services := l_service_ids.COUNT;

  FOR order_rec IN (SELECT order_id FROM STUDIO_RENT_ORDERS) LOOP
    l_assigned.DELETE;

    FOR j IN 1..TRUNC(DBMS_RANDOM.VALUE(1, LEAST(5, v_max_services)+1)) LOOP
      LOOP
        v_service_id :=
          l_service_ids(TRUNC(DBMS_RANDOM.VALUE(1, v_max_services)+1));
        EXIT WHEN NOT l_assigned.EXISTS(v_service_id);
      END LOOP;

      l_assigned(v_service_id) := 1;

      BEGIN
        INSERT INTO ORDER_SERVICES(order_id, service_id)
        VALUES(order_rec.order_id, v_service_id);
      EXCEPTION
        WHEN DUP_VAL_ON_INDEX THEN
          NULL;
      END;
    END LOOP;
  END LOOP;

  COMMIT;
END;
/

-- Генерация архивных заказов
CREATE OR REPLACE PROCEDURE gen_archived_orders IS
  v_order_id   NUMBER;
  v_user_id    NUMBER;
  v_studio_id  NUMBER;
  v_service_id NUMBER;

  TYPE t_num_tab IS TABLE OF NUMBER INDEX BY PLS_INTEGER;

  l_user_ids    t_num_tab;
  l_studio_ids  t_num_tab;
  l_service_ids t_num_tab;
  l_assigned    t_num_tab;

  v_count PLS_INTEGER;
BEGIN
  SELECT user_id   BULK COLLECT INTO l_user_ids   FROM USERS;
  SELECT studio_id BULK COLLECT INTO l_studio_ids FROM STUDIOS;
  SELECT service_id BULK COLLECT INTO l_service_ids FROM SERVICES;

  FOR i IN 1..10000 LOOP
    v_user_id :=
      l_user_ids(TRUNC(DBMS_RANDOM.VALUE(1, l_user_ids.COUNT+1)));
    v_studio_id :=
      l_studio_ids(TRUNC(DBMS_RANDOM.VALUE(1, l_studio_ids.COUNT+1)));

    INSERT INTO ARCHIVED_STUDIO_RENT_ORDERS
      (user_id, studio_id, rent_date, start_time, end_time, total_price, archived_at)
    VALUES (
      v_user_id,
      v_studio_id,
      SYSDATE - TRUNC(DBMS_RANDOM.VALUE(1, 365)),
      SYSTIMESTAMP,
      SYSTIMESTAMP + INTERVAL '2' HOUR,
      DBMS_RANDOM.VALUE(100, 2000),
      SYSDATE
    )
    RETURNING order_id INTO v_order_id;

    l_assigned.DELETE;
    v_count :=
      TRUNC(DBMS_RANDOM.VALUE(1, LEAST(5, l_service_ids.COUNT)+1));

    WHILE l_assigned.COUNT < v_count LOOP
      v_service_id :=
        l_service_ids(TRUNC(DBMS_RANDOM.VALUE(1, l_service_ids.COUNT+1)));

      IF NOT l_assigned.EXISTS(v_service_id) THEN
        l_assigned(v_service_id) := 1;
        INSERT INTO ARCHIVED_ORDER_SERVICES(order_id, service_id)
        VALUES(v_order_id, v_service_id);
      END IF;
    END LOOP;
  END LOOP;

  COMMIT;
END;
/

-- Заполнение таблиц
BEGIN
  gen_users;
  gen_studios;
  gen_services;
  gen_studio_orders;
  gen_order_services;
  gen_archived_orders;
END;
/

--------------------------------------------------------------------------------
-- Очистка таблиц 
--------------------------------------------------------------------------------

CREATE OR REPLACE PROCEDURE reset_all_data IS
BEGIN
  -- Отключение внешних ключей
  EXECUTE IMMEDIATE 'ALTER TABLE ADMINS DISABLE CONSTRAINT fk_admin_user';
  EXECUTE IMMEDIATE 'ALTER TABLE STUDIO_RENT_ORDERS DISABLE CONSTRAINT fk_sro_user';
  EXECUTE IMMEDIATE 'ALTER TABLE STUDIO_RENT_ORDERS DISABLE CONSTRAINT fk_sro_studio';
  EXECUTE IMMEDIATE 'ALTER TABLE ARCHIVED_STUDIO_RENT_ORDERS DISABLE CONSTRAINT fk_asro_user';
  EXECUTE IMMEDIATE 'ALTER TABLE ARCHIVED_STUDIO_RENT_ORDERS DISABLE CONSTRAINT fk_asro_studio';
  EXECUTE IMMEDIATE 'ALTER TABLE ORDER_SERVICES DISABLE CONSTRAINT fk_os_order';
  EXECUTE IMMEDIATE 'ALTER TABLE ORDER_SERVICES DISABLE CONSTRAINT fk_os_service';

  -- Удаление данных
  DELETE FROM ARCHIVED_ORDER_SERVICES;
  DELETE FROM ORDER_SERVICES;
  DELETE FROM ARCHIVED_STUDIO_RENT_ORDERS;
  DELETE FROM STUDIO_RENT_ORDERS;
  DELETE FROM ADMINS;
  DELETE FROM SERVICES;
  DELETE FROM STUDIOS;
  DELETE FROM USERS;

  COMMIT;

  -- Включение внешних ключей
  EXECUTE IMMEDIATE 'ALTER TABLE ADMINS ENABLE CONSTRAINT fk_admin_user';
  EXECUTE IMMEDIATE 'ALTER TABLE STUDIO_RENT_ORDERS ENABLE CONSTRAINT fk_sro_user';
  EXECUTE IMMEDIATE 'ALTER TABLE STUDIO_RENT_ORDERS ENABLE CONSTRAINT fk_sro_studio';
  EXECUTE IMMEDIATE 'ALTER TABLE ARCHIVED_STUDIO_RENT_ORDERS ENABLE CONSTRAINT fk_asro_user';
  EXECUTE IMMEDIATE 'ALTER TABLE ARCHIVED_STUDIO_RENT_ORDERS ENABLE CONSTRAINT fk_asro_studio';
  EXECUTE IMMEDIATE 'ALTER TABLE ORDER_SERVICES ENABLE CONSTRAINT fk_os_order';
  EXECUTE IMMEDIATE 'ALTER TABLE ORDER_SERVICES ENABLE CONSTRAINT fk_os_service';

  -- Сброс identity-счётчиков
  EXECUTE IMMEDIATE
    'ALTER TABLE USERS MODIFY user_id GENERATED BY DEFAULT ON NULL AS IDENTITY (START WITH 1)';
  EXECUTE IMMEDIATE
    'ALTER TABLE ADMINS MODIFY admin_id GENERATED BY DEFAULT ON NULL AS IDENTITY (START WITH 1)';
  EXECUTE IMMEDIATE
    'ALTER TABLE STUDIOS MODIFY studio_id GENERATED BY DEFAULT ON NULL AS IDENTITY (START WITH 1)';
  EXECUTE IMMEDIATE
    'ALTER TABLE STUDIO_RENT_ORDERS MODIFY order_id GENERATED BY DEFAULT ON NULL AS IDENTITY (START WITH 1)';
  EXECUTE IMMEDIATE
    'ALTER TABLE ARCHIVED_STUDIO_RENT_ORDERS MODIFY order_id GENERATED BY DEFAULT ON NULL AS IDENTITY (START WITH 1)';
  EXECUTE IMMEDIATE
    'ALTER TABLE SERVICES MODIFY service_id GENERATED BY DEFAULT ON NULL AS IDENTITY (START WITH 1)';

END reset_all_data;
/

BEGIN
  reset_all_data;
END;
/

--------------------------------------------------------------------------------
-- Удаление таблиц 
--------------------------------------------------------------------------------

CREATE OR REPLACE PROCEDURE drop_all_tables IS
BEGIN
  BEGIN
    EXECUTE IMMEDIATE 'DROP TABLE ORDER_SERVICES CASCADE CONSTRAINTS';
  EXCEPTION
    WHEN OTHERS THEN NULL;
  END;

  BEGIN
    EXECUTE IMMEDIATE 'DROP TABLE ARCHIVED_ORDER_SERVICES CASCADE CONSTRAINTS';
  EXCEPTION
    WHEN OTHERS THEN NULL;
  END;

  BEGIN
    EXECUTE IMMEDIATE 'DROP TABLE ARCHIVED_STUDIO_RENT_ORDERS CASCADE CONSTRAINTS';
  EXCEPTION
    WHEN OTHERS THEN NULL;
  END;

  BEGIN
    EXECUTE IMMEDIATE 'DROP TABLE STUDIO_RENT_ORDERS CASCADE CONSTRAINTS';
  EXCEPTION
    WHEN OTHERS THEN NULL;
  END;

  BEGIN
    EXECUTE IMMEDIATE 'DROP TABLE SERVICES CASCADE CONSTRAINTS';
  EXCEPTION
    WHEN OTHERS THEN NULL;
  END;

  BEGIN
    EXECUTE IMMEDIATE 'DROP TABLE STUDIOS CASCADE CONSTRAINTS';
  EXCEPTION
    WHEN OTHERS THEN NULL;
  END;

  BEGIN
    EXECUTE IMMEDIATE 'DROP TABLE ADMINS CASCADE CONSTRAINTS';
  EXCEPTION
    WHEN OTHERS THEN NULL;
  END;

  BEGIN
    EXECUTE IMMEDIATE 'DROP TABLE USERS CASCADE CONSTRAINTS';
  EXCEPTION
    WHEN OTHERS THEN NULL;
  END;
END drop_all_tables;
/

BEGIN
  drop_all_tables;
END;
/